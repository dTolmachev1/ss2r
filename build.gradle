plugins {
    id 'java'
    id 'application'
}

group = 'io.github.dtolmachev1'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    commonscliVersion = '1.6.0'
    jakartaAnnotationVersion = '2.1.1'
    junitVersion = '5.9.3'
    opencsvVersion = '5.9'
    postgresqlVersion = '42.5.4'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'io.github.dtolmachev1.ConsoleApp'
}

dependencies {
    implementation "com.opencsv:opencsv:${opencsvVersion}"
    implementation "commons-cli:commons-cli:${commonscliVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"
    implementation "org.postgresql:postgresql:${postgresqlVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes 'main-class': application.mainClass
    }
    archiveClassifier = 'standalone'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

build {
    dependsOn fatJar
}
